class Compiler {
    static function compileFiles(options: Options) {
        let files: string[] = options.files
        mutable sources: string[]$ = new string[]()
        for file: string in files {
            let source: string@sources = File.readToString(file)
                catch FileError.noSuchFileOrDirectory(error: noSuchFileOrDirectory) {
                    mutable msg: VarString$ = new VarString("Can't read file: ")
                    msg.append(file)
                    msg.append("\n")
                    let message: string$ = new string(msg)
                    print(message) catch _ return
                    return
                }
                
            sources.push(source)
        }

        mutable compilationUnits: CompilationUnit[]$ = new CompilationUnit[]()
        let index: number = 0
        for source: string in sources {
            let moduleName: string@compilationUnits = Path.getFileNameWithoutExtension(files[index])
            let compilationUnit: CompilationUnit@compilationUnits = parseUnit(moduleName, source)
            if compilationUnit == null {
                mutable msg: VarString$ = new VarString("Syntax error in ")
                msg.append(files[index])
                msg.append("\n")
                let message: string$ = new string(msg)
                print(message) catch _ return
                return
            }

            compilationUnits.push(compilationUnit)
            index++
        }

        let program: Program$ = new Program(options.outputName, new CompilationUnit[](compilationUnits))
        for item: CompilationUnit in compilationUnits
            item.parent = program

        mutable headerVisitor: HeaderVisitor$ = new HeaderVisitor(options.directory)        
        headerVisitor.execute(program)

        mutable sourceVisitor: SourceVisitor$ = new SourceVisitor(options.directory)        
        sourceVisitor.execute(program)
    }

    static function parseUnit(moduleName: string, text: string): CompilationUnit {
        mutable parser: Parser$ = new Parser(moduleName, text)
        let compilationUnit: CompilationUnit = parser.parseCompilationUnit()
        return(compilationUnit)
    }
}
